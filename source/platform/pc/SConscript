#
# Copyright (C) 2011 Alex Smith
#
# Permission to use, copy, modify, and/or distribute this software for any
# purpose with or without fee is hereby granted, provided that the above
# copyright notice and this permission notice appear in all copies.
#
# THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
# WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
# MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
# ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
# WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
# ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
# OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.
#

Import('env', 'defaults')

sources = map(File, [
	'loaders/chainload.c',
	'loaders/enter.S',
	'bios.S',
	'console.c',
	'disk.c',
	'memory.c',
	'platform.c',
	'pxe.c',
	'start.S',
	'video.c',
])

# Build the stage 1 loaders.
stage1s = ['cdboot', 'ext2boot', 'pxeboot']
for f in stage1s:
	env.Program('stage1/%s.elf' % (f), 'stage1/%s.S' % (f), LIBS = [], LINKFLAGS = env['LINKFLAGS'] + [
		'-Wl,-T,${SOURCE.srcdir}/stage1.ld',
		'-Wl,-N'
	])
	Depends('stage1/%s.elf' % (f), File('stage1/stage1.ld'))
	target = env.Command('stage1/%s' % (f), 'stage1/%s.elf' % (f), Action(
		"$OBJCOPY -O binary --strip-all -R '.note.*' -R .comment $SOURCE $TARGET",
		'$GENCOMSTR'
	))
	defaults.append(target)

# Set the linker script path.
env['LDSCRIPT'] = env.LDScript('link.ld', 'link.ld.in')

Return('sources')
